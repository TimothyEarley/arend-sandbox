\import Data.Bool
\import Equiv
\import Function
\import Logic
\import Meta
\import Paths
\import Paths.Meta

\func \infix 6 <--> (A B : \Type) => \Sigma (A -> B) (B -> A)

  \where {
    \func refl {A : \Type} : A <--> A => (id, id)
    \func symm {A B : \Type} (h : A <--> B) : B <--> A => (h.2, h.1)
    \func trans {A B C : \Type} (h1 : A <--> B) (h2 : B <--> C) : A <--> C => (h2.1 o h1.1, h1.2 o h2.2)

    \func \infixr 1 --> {A B C : \Type} (h : A <--> B) (h' : B <--> C) => trans h h'
    \func \infix 2 <-- (A : \Type) {B : \Type} (h : A <--> B) => h
    \func \fix 2 done (A : \Type) : A <--> A => refl

  }

\func <->refl {A : \Prop} : A <-> A => <-->.refl
\func <->symm {A B : \Prop} (h : A <-> B) : B <-> A => <-->.symm h
\func <->trans {A B C : \Prop} (h1 : A <-> B) (h2 : B <-> C) : A <-> C => <-->.trans h1 h2

\func invN {A : \Type} {a b : A} (h : Not (a = b)) : Not (b = a) => h o inv

\func invPi {A : \Type} {B : A -> \Type} {f1 f2 : \Pi (a : A) -> B a} (f : \Pi (a : A) -> f1 a = f2 a)
  : \Pi (a : A) -> f2 a = f1 a
  => \lam a => inv (f a)

\func orTrue {b : Bool} : or b true = true \elim b
  | true => idp
  | false => idp

\func trueOr {b : Bool} : or true b = true \elim b
  | true => idp
  | false => idp

\func andFalse {b : Bool} : b and false = false \elim b
  | false => idp
  | true => idp

\func andTrue {b : Bool} : b and true = b \elim b
  | false => idp
  | true => idp

\func deMorganOr {a b : Bool} : not (a or b) = not a and not b
  => cases (a, b) idp

\func notNot {a : Bool} : not (not a) = a
  => cases a idp


\func absurdExt {A B : \Type} (ha : A -> Empty) (hb : B -> Empty) : A = B => ext (\new QEquiv {
  | f a => absurd (ha a)
  | ret b => absurd (hb b)
  | ret_f a => absurd (ha a)
  | f_sec b => absurd (hb b)
})